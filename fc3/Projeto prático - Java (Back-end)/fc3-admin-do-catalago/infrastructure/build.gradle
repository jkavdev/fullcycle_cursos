plugins {
    id 'java-conventions'
    id 'jacoco-report-aggregation'
//    adicionando plugin que fornece facilidades para rodar a aplicacao
    id 'application'
    //    plugins do spring e spring boot
    id 'org.springframework.boot' version '2.7.9'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.flywaydb.flyway' version '9.12.0'
}

group 'br.com.jkavdev.fullcycle.admin.catalogo.infrastructure'

bootJar {
    archiveName 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation 'com.google.cloud:google-cloud-storage:2.20.1'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'mysql:mysql-connector-java:8.0.32'

    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.15'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.15'

//    utilizando o servidor undertow, por se mostrar mais performatico que o tomcat
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    implementation 'com.fasterxml.jackson.module:jackson-module-afterburner'

//    a versao 2.0 da erro
    implementation('org.yaml:snakeyaml:1.33') {
        version {strictly('1.33')}
    }

    testImplementation(project(path: ':domain', configuration: 'testClasses'))

    testImplementation 'org.flywaydb:flyway-core'
    testImplementation 'org.flywaydb:flyway-mysql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.springframework.amqp:spring-rabbit-test:2.4.10')

    testImplementation 'org.testcontainers:testcontainers:1.17.6'
    testImplementation 'org.testcontainers:mysql:1.17.6'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'

    testRuntimeOnly 'com.h2database:h2'
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_videos'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '123456'
    cleanDisabled = false
}

testCodeCoverageReport {
    reports {
        xml {
            enabled true
            destination file("$rootDir/build/reports/jacoco/test/jacocoTestReport.xml")
        }

        html {
            enabled true
            destination file("$rootDir/build/reports/jacoco/test/")
        }
    }
}

tasks.named("jacocoTestReport") {
    dependsOn tasks.named("testCodeCoverageReport", JacocoReport)
}